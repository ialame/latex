 ************************************************************************ 
 * Poutre 1D elastique en bois 
 ************************************************************************ 
 * 
 * fichier poutre1D.dgibi 
 * 
 TITR 'Poutre 1D élastique en bois' ; 
 OPTI DIME 2 ELEM SEG2 ; 
 * 
 * Données du problème (Unités : m, MPa, MN) 
 * 
 F1 = -1.5E-3 ; 
 L1 = 2.00 ; 
 h1 = 0.25 ; 
 b1 = 0.06 ; 
 E1 = 1.2E4 ; 
 nu1= 0.3 ; 
 Iyy1 = (b1**3)*h1/12 ; 
 Izz1 =(h1**3)*b1/12 ; 
 I0=Iyy1 + Izz1 ; 
 S1=b1 * h1; 
 * 
 * Solution analytique RdM 
 * 
 fana0 =(F1*(L1**3)/(3*E1*Izz1))*1000 ; 
 * 
 * Géométrie 
 * 
 P1=0. 0. ; 
 P2 = L1 0. ; 
 Nelt1 = 10 ; 
 Poutre1 = P1 DROI Nelt1 P2 ; 
 TRAC Poutre1; 
 * 
 * Modèle (Comportement et modélisation EF) 
 * 
 mo1=MODE Poutre1 MECANIQUE ELASTIQUE ISOTROPE POUT ; 
 * 
 * Données matériau et élément 
 * 
 ma1=MATE mo1 YOUN E1 SECT S1 NU nu1 INRZ IZZ1; 
 * 
 * Conditions aux limites 
 * 
 Cond1 =BLOQ UX UY RZ P1 ; 
 * 
 * Chargement 
 * 
 Force1=FORC (0. -100000) P2 ; 
 * 
 * Résolution 
 * 
 Rigi1=RIGI mo1 ma1 ; 
 RigiCL1=Rigi1 ET Cond1 ; 
 Depl1=RESO RigiCL1 Force1 ; 
 * 
 * Poste Traitement 
 * 
 Fleche1=EXTR Depl1 UY P2 ; 
 Fleche1=1e3*Fleche1 ; 
 MESS 'Flèche CASTEM : ' Fleche1 'mm' ; 
 MESS 'Flèche analytique : ' fana0 'mm' ; 
 * 
 Def0=DEFO Poutre1 Depl1 0. BLAN ; 
 Def1=DEFO Poutre1 Depl1 ROUG ; 
 TRAC (Def0 ET Def1) ; 
 * 
 * Fin du fichier 
 * 
 FIN ; 
